// MDW automated tests (build must have been run already) 

def getMdwCmd() {
    if (System.properties['os.name'].startsWith('Windows')) {
        return 'cli\\bin\\mdw.bat'
    }
    else {
        return 'cli/bin/mdw'
    }
}

task env {
    tasks.withType(Exec) {
        environment << [MDW_HOME: 'cli']
    }
}

task install(type: Copy) {
    from "deploy/app/mdw-boot-${mdwVersion}.jar"
    into 'bin'
}

task start(type: Exec) {
    dependsOn install
    commandLine getMdwCmd(), 'run', '--daemon', '--wait=120'
}

def testSuccess = true

task stop(type: Exec) {
    commandLine getMdwCmd(), 'stop'
    doLast {
        if (!testSuccess)
            ant.fail('TEST FAILURE(S)')
    }
}

task allTests(type: Exec) {
    commandLine getMdwCmd(), 'test', '--stubbing'
    ignoreExitValue true
    doLast {
        testSuccess = (execResult.exitValue == 0)
    }
}
task testAll(dependsOn: [start, allTests, stop]) {
}
allTests.mustRunAfter start
stop.mustRunAfter allTests

// TODO: random tests