// publishes build output to a maven repository
// eg: mdw repo or maven central

repositories {
    mavenCentral()
}

configurations {
    publish
}

dependencies {
    publish files("../${mdwOutputDir}/mdw-common-${mdwVersion}.jar")
    publish group: "org.apache.xmlbeans", name: "xmlbeans", version: "2.4.0"
    publish group: "org.json", name: "json", version: "20090211"
    publish group: "org.eclipse.jgit", name: "org.eclipse.jgit", version: "3.4.1.201406201815-r"
}

task buildBaseConfigs() {
    doLast {
        def appConfigXml = file("../mdw-common/src/META-INF/mdw/spring/application-context.xml")
        file("../${mdwOutputDir}/application-context-${mdwVersion}.xml").bytes = appConfigXml.bytes;
    }
}

task cleanPackages(type: Delete) {
    delete fileTree(dir: "../${mdwAssetOutputDir}")
}

// export all the packages in the mdw-workflow/assets directory
task exportAssetPackages {
    description = "Export Asset Packages"

    // superCats are categorized as the parent package 
    // (eg com.centurylink.mdw.tests.camel is category "tests" instead of "camel")
    def superCats = ["services", "tests"]
    
    def assetRootPath = file("../${mdwAssetRoot}").toString()
    def mdwPkgVer = mdwVersion;
    if (mdwVersion.endsWith("-SNAPSHOT"))
        mdwPkgVer = mdwPkgVer.substring(0, mdwPkgVer.length() - 9)
    println "Exporting asset packages from " + assetRootPath + ":" 
    fileTree(dir: "../${mdwAssetRoot}", include:'**/.mdw/package.json', exclude:'Archive/**/*').each { pkgFile ->
        doLast {
            def pkgName = pkgFile.parentFile.parent.substring(assetRootPath.length() + 1).replace('/','.').replace('\\','.')
            def pkgCat = pkgName.substring(pkgName.lastIndexOf('.') + 1)
            superCats.find { 
                if (pkgName.endsWith(it + "." + pkgCat)) {
                    pkgCat = it
                    return true
                }
                return false
            }
            println "    " + pkgCat + ": " + pkgName
            println "        setting package.json version: ${mdwPkgVer}"
            pkgFile.setWritable(true)
            def pkgJson = pkgFile.text
            pkgFile.withWriter { w ->
                w << pkgJson.replaceAll("\\\"version\\\": \\\".*?\\\"", "\"version\": \"${mdwPkgVer}\"");
            }
            if (pkgName.equals("com.centurylink.mdw.db")) {
                javaexec {
                    maxHeapSize "1280m"
                    classpath configurations.publish
                    main = "com.centurylink.mdw.dataaccess.file.ImporterExporterJson"
                    args "../${mdwAssetRoot}", pkgName, "../${mdwAssetOutputDir}/${pkgCat}/${pkgName}-${mdwVersion}.json"
                }
            }
            else {
                javaexec {
                    classpath configurations.publish
                    main = "com.centurylink.mdw.dataaccess.file.ImporterExporterJson"
                    args "../${mdwAssetRoot}", pkgName, "../${mdwAssetOutputDir}/${pkgCat}/${pkgName}-${mdwVersion}.json"
                }
            }
        }
    }
}
exportAssetPackages.mustRunAfter cleanPackages

task buildPackages(type: Copy) {
    dependsOn(cleanPackages, exportAssetPackages)
}

task checkAllowed() {
    doLast {
        assert publishUser != null : "Missing property: publishUser"
        assert publishPassword != null : "Missing property: publishPassword"
        // TODO disallow non-snapshot publish except from Jenkins
    }
}

task publishBase(type: HttpPut) {
    dependsOn(checkAllowed, buildBaseConfigs)
    from "../${mdwOutputDir}/mdw-config-${mdwVersion}.zip"
    from "../${mdwOutputDir}/application-context-${mdwVersion}.xml"
    baseUrl "${publishBaseUrl}/mdw/${mdwVersion}"
    user publishUser
    password publishPassword
}

task publishWar(type: HttpPut) {
    dependsOn checkAllowed
    from "../${mdwOutputDir}/mdw-${mdwVersion}.war"
    baseUrl "${publishBaseUrl}/mdw/${mdwVersion}"
    withSources true
    user publishUser
    password publishPassword
}

// must be run after build.gradle target buildAll
task publishJars(type: HttpPut) {
    dependsOn(checkAllowed)
    from "../${mdwOutputDir}/mdw-common-${mdwVersion}.jar"
    from "../${mdwOutputDir}/mdw-services-${mdwVersion}.jar"
    from "../${mdwOutputDir}/mdw-workflow-${mdwVersion}.jar"
    groupUrl "${publishBaseUrl}"
    withSources true
    user publishUser
    password publishPassword
}

// TODO: consider using standard Maven repository structure
// (especially if we end up rehosting asset discovery to a different repo)
task publishAssets(type: HttpPut) {
    dependsOn(checkAllowed, buildPackages)
    from "../${mdwAssetOutputDir}"
    assetsUrl "${publishBaseUrl}/assets"
    user publishUser
    password publishPassword
}

task publishBuild(dependsOn: [publishBase, publishJars, publishWar, publishAssets]) {
    doLast {
        println "*** MDW build ${mdwVersion} published to ${publishBaseUrl} ***"
    }
}

task publishToMavenCentral(type: GradleBuild) {
      buildFile = "build.gradle"
      tasks = ["uploadArchives"]
  }

def createReleaseTag = {
      def tagName = "release/${mdwVersion}"
      ("git tag $tagName").execute()
      ("git push origin $tagName").execute()
      return "$tagName"
  }
  
 
task tagRelease(type: GradleBuild){
    doLast {
        println createReleaseTag()
    }
}