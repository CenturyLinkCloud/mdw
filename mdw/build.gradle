apply plugin: 'maven'
apply plugin: 'signing'

configurations { 
    assets 
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse-wtp"
    apply plugin: 'maven'
    apply plugin: 'signing'

    eclipse {
        classpath {
            defaultOutputDir = file("build/classes")
        }
    }

    sourceCompatibility = 1.8
    version = "${mdwVersion}"

    libsDirName = "../../${mdwOutputDir}"

    sourceSets {
        main {
            java {
                srcDir "src"
            }
            output.classesDir = "build/classes"
        }
    }

    repositories {
        mavenCentral()
    }
    if (mdwVersion.endsWith("-SNAPSHOT"))
            tasks.withType(Javadoc).all { enabled = false }

    // copy into output dir rather than including directly in jar
    // this works better in local development where deployment happens from build/classes
    task copyMeta(type: Copy) {
        from("src") {
            include "META-INF/mdw/**"
            include "META-INF/spring/**"
            exclude "META-INF/**/.metadata"
            exclude "META-INF/**/.*ignore"
        }
        into sourceSets.main.output.classesDir
    }

    processResources {
        dependsOn copyMeta
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
        exclude "META-INF"
        exclude "**/.metadata/"
        exclude "**/.*ignore"
    }

    def packaging = 'jar'

    task javadocJar(type: Jar) {
        if (!mdwVersion.endsWith("-SNAPSHOT"))
        enabled = "true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))
        classifier = 'javadoc'
        from javadoc
    }
}

task cleanJavadocs(type: Delete) {
    enabled = "true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))
    delete "../docs/_docs/javadoc"
}

task packageSources(type: GradleBuild) {
    buildFile = "build.gradle"
    tasks = ["sourcesJar"]
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocs(type: Javadoc) {
    if (!mdwVersion.endsWith("-SNAPSHOT"))
        enabled = "true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))
    dependsOn "cleanJavadocs"
    source subprojects.collect {project -> project.sourceSets.main.java }
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath})
    title = "MDW 6 API JavaDocs"
    maxMemory = "256m"
    options.links "http://docs.oracle.com/javase/8/docs/api/"
    options.bottom "<i>Copyright &#169; 2017 CenturyLink, Inc.</i>"
    destinationDir = file('../docs/_docs/javadoc')
}

task zipAssetPackages(type: GradleBuild) {
    buildFile = "publish.gradle"
    tasks = ["zipAllAssets"]
    startParameter.projectProperties = [publishBaseUrl: "n/a", publishUser: "n/a", publishPassword: "n/a" ]
}

task updateRestApiDefinition(type: GradleBuild) {
    buildFile = "publish.gradle"
    tasks = ["updateRestApiDefinition"]
    startParameter.projectProperties = [publishBaseUrl: "n/a", publishUser: "n/a", publishPassword: "n/a" ]
}

task updateMdwVerInFiles(type: GradleBuild) {
    buildFile = "publish.gradle"
    tasks = ["updateMdwVerInFiles"]
    startParameter.projectProperties = [publishBaseUrl: "n/a", publishUser: "n/a", publishPassword: "n/a" ]
}

task versions {
    doLast {
        println "Java Version: " + System.getProperty("java.version")
        println GradleVersion.current().prettyPrint()
        println "MDW Version: ${mdwVersion}"
    }
}

// deploy a build for testing on lxdenvmtc143
task deployTomcatMdwDev(type: GradleBuild) {
    buildFile = "deploy.gradle"
    tasks = ["deployMdw"]
    startParameter.projectProperties = [deployWarDir: "/prod/ecom2/local/apps/tomcat/tomcat7-mdw6/webapps",
                                        tomcatBinDir: "/prod/ecom2/local/apps/tomcat/tomcat7-mdw6/bin",
                                        testServerUrl: "http://lxdenvmtc143.dev.qintra.com:8515/mdw/Services/AppSummary"]
}

task publishToMavenCentral(type: GradleBuild) {
    dependsOn javadocs
    buildFile = "publish.gradle"
    tasks = ["publishToMavenCentral"]
    startParameter.projectProperties = [publishBaseUrl: "n/a", publishUser: "n/a", publishPassword: "n/a" ]
}

allprojects {
    group = "com.centurylink.mdw"
    version = "${mdwVersion}"
    ext.ossrhUsername = System.getProperty("ossrhUsername")
    ext.ossrhPassword = System.getProperty("ossrhPassword")

    if("true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))) {
        signing {
            required = { "true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))}
            sign configurations.archives
         }
    }
    else {
        tasks.withType(Javadoc).all { enabled = false }
    }

    uploadArchives {
        enabled = "true".equalsIgnoreCase(System.getProperty("PUBLISHING_TO_MAVEN_CENTRAL"))
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: mavenRepoUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: mavenSnapshotRepoUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name 'MDW Templates'
                    groupId 'com.centurylink.mdw'
                    version = mdwVersion
                    packaging = 'zip'
                    description 'MDW is a cloud workflow framework'
                    url 'http://centurylinkcloud.github.io/mdw/'
                    scm {
                        connection 'scm:git:git://github.com/CenturyLinkCloud/mdw.git'
                        developerConnection 'scm:git:ssh://github.com:CenturyLinkCloud/mdw.git'
                        url 'https://github.com/CenturyLinkCloud/mdw.git'
                    }
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'mdwdev'
                            name 'MDW Development'
                            email 'mdwdev@centurylink.com'
                        }
                    }
                }
            }
        }
    }
}

/**
 * The artifact for this build is mdw-templates (only because each maven artifact
 * needs a build project, and we don't want one especially for templates).
 */
task zipTemplates(type: Zip) {
    destinationDir = file("../${mdwOutputDir}")
    archiveName = "mdw-templates-${mdwVersion}.zip"
    from "templates"
    include "**/*"
}
artifacts {
    archives zipTemplates
}
uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                artifactId = "mdw-templates"
            }
        }
    }
}

task buildAll(type: GradleBuild) {
    dependsOn zipTemplates
    buildFile = "build.gradle"
    tasks = ["build"]
}

task archiveAssetZips {
    dependsOn zipAssetPackages
    doLast{
        ext.discoveredFiles = []
        fileTree(dir: "../${mdwAssetOutputDir}", include:'**/*.zip').each { file ->
            discoveredFiles.add(file)
                artifacts{
                 assets file:file 
                }
            }
        }
    }

task configureUploadAssets {
    dependsOn archiveAssetZips
    doLast{
        uploadAssets {
            ext.ossrhUsername = System.getProperty("ossrhUsername")
            ext.ossrhPassword = System.getProperty("ossrhPassword")
            repositories {
                mavenDeployer {
                    repository(url: mavenRepoUrl) {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    snapshotRepository(url: mavenSnapshotRepoUrl) {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    archiveAssetZips.discoveredFiles.each { discoveredFile ->
                        def filterName = discoveredFile.name - ".zip"
                        def artifactName = filterName - "-SNAPSHOT"
                        def versionStart = artifactName.lastIndexOf("-")
                        def artifactId = artifactName.substring(0, versionStart)
                        addFilter(filterName) { artifact, file ->
                            file.name == discoveredFile.name
                        }
                        pom(filterName).artifactId = artifactId
                        pom(filterName).groupId = "com.centurylink.mdw.assets"
                    }
                }
            }
        }
    }
}

uploadAssets.dependsOn configureUploadAssets

task cleanAll(type: GradleBuild) {
    dependsOn cleanJavadocs
    buildFile = "build.gradle"
    tasks = ["clean"]
}

// update mdw-demo workspace locally during the build. local.gradle is in our internal GitLab repo, update mdwDemoDir
task updateMDWDemoWorkspace(type: GradleBuild) {
    buildFile = "local.gradle"
    tasks = ["updateMDWDemoWorkspace"]
    startParameter.projectProperties = [deployWarDir: "n/a", tomcatBinDir: "n/a", testServerUrl: "n/a"]
}

