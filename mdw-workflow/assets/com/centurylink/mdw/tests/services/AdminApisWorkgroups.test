package com.centurylink.mdw.tests.services

import groovy.json.JsonSlurper

def vals = [
    'group-name': 'GroupA',
    'group-description': 'a group for testing'
]

// initially no capture (possible cleanup)
def GET = request('admin-apis.postman/GET:workgroups/{group-name}') {
    values = vals
    options = [ captureResult: false ]
}
def DEL = request('admin-apis.postman/DEL:workgroups/{group-name}') {
    values = vals
    options = [ captureResult: false ]
}

def response = submit GET

if (response.status.code == 200) {
    // delete the workgroup so we can submit a POST
    response = submit DEL
    assert response.status.code == 200
    response = submit GET
    assert response.status.code == 404
}

// now start capturing
GET.options['captureResult'] = DEL.options['captureResult'] = true

// create workgroup
def POST = request('admin-apis.postman/POST:workgroups') {
  values = vals
}

response = submit POST
assert response.status.code == 201
assert response.time < 1000 // less than a second
// case-insensitive header check
assert response.checkHeader('content-type', 'application/json')

// ensure no dups
response = submit POST
assert response.status.code == 409

// update workgroup
def PUT = request('admin-apis.postman/PUT:workgroups/{group-name}') {
  values = vals
}
response = submit PUT
// confirm update
response = submit GET
def workgroup = new JsonSlurper().parseText(response.body)
assert workgroup.description == 'a group for testing (updated)'

verify(response) {
  values = vals
}
