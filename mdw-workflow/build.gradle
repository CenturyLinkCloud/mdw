eclipse {
    classpath {
        defaultOutputDir = file("build/classes")
        containers "GROOVY_SUPPORT"
        containers "GROOVY_DSL_SUPPORT"
        file {
            whenMerged { classpath ->
                // eclipse should process dynamic java, but should not be part of build
                def assetsSrc = new org.gradle.plugins.ide.eclipse.model.SourceFolder("assets", "build/assets")
                assetsSrc.excludes.add("Archive/")
                entries.add(assetsSrc)
                
                def groovySupport = entries.find { it.kind == "con" && it.path == "GROOVY_SUPPORT" }
                groovySupport.entryAttributes.put("minimal", "true")
            }
        }
    }
    wtp {
        facet {
            facet name: "mdw.workflow", version: "5.1"
            facet type: "fixed", name: "jst.utility"
            facet name: "jst.utility", version: "1.0"
        }
    }
}

dependencies {
    compile project(":mdw-common")
    compile project(":mdw-services")
    compile group: "org.apache.velocity", name: "velocity", version: "1.6.4"
    compile group: "org.apache.velocity", name: "velocity-tools", version: "1.4"
    compile group: "org.apache.poi", name: "poi-all", version: "3.9"
    
	compile group: "org.drools", name: "drools-core", version: "5.6.0.Final"
	compile group: "org.drools", name: "drools-templates", version: "5.6.0.Final"
	compile group: "org.drools", name: "drools-decisiontables", version: "5.6.0.Final"
	compile group: "org.drools", name: "drools-compiler", version: "5.6.0.Final"
    compile group: "org.drools", name: "knowledge-api", version: "5.6.0.Final"
	compile group: "org.drools", name: "knowledge-internal-api", version: "5.6.0.Final"
	
	compile group: "org.mvel", name: "mvel2", version: "2.1.8.Final"
	compile group: "org.antlr", name: "antlr-runtime", version: "3.3"
	
    compile group: "javax.connector", name: "connector", version: "1.5"
    compile group: "junit", name: "junit", version:"4.8.1"
    
    compile group: "expect4j", name: "expect4j", version: "1.1.0"
    compile group: "oro", name: "oro", version: "2.0.8"
    
    // TODO: proprietary dependencies
    compile group: "com.qwest.bus", name: "bus-all", version: "2.1.2.1"
    compile group: "com.ibm", name: "com.ibm.mq", version: "7.0.1.9"
    compile group: "com.ibm", name: "com.ibm.mq.jmqi", version: "7.0.1.9"
    
}

task cleanJar << {
    delete "../${mdwOutputDir}/mdw-base-${version}.jar"
}

clean {
    dependsOn cleanJar
}

jar {
    manifest {
        attributes("MDW-Version": mdwVersion)
        attributes("MDW-Build": new Date().format(mdwBuildTimeFormat))
    }
}