sourceSets {
    main {
        java {
            srcDirs "src"
            srcDirs "assets"
        }
        output.classesDir = "build/classes"
    }
}

dependencies {
    compile project(":mdw-common")
    compile project(":mdw-services")
    
    // build-time-only dependencies (exluded from mdw-hub)
    compile fileTree(dir: "assets/com/centurylink/mdw/drools", includes: ["*.jar"])   
    compile fileTree(dir: "assets/com/centurylink/mdw/camel", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/node", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/kafka", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/kotlin", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/mongo", includes: ["*.jar"])   
    compile fileTree(dir: "assets/com/centurylink/mdw/microservice", includes: ["*.jar"])
    
    compile group: "ch.vorburger.mariaDB4j", name: "mariaDB4j-core", version: "2.2.3"
    compile group: "org.apache.velocity", name: "velocity", version: "1.7"
    
    compile group: "junit", name: "junit", version:"4.8.1"
    compile group: "oro", name: "oro", version: "2.0.8"
}

task cleanJar {
    doLast {
        delete "../${mdwOutputDir}/mdw-base-${version}.jar"
    }
}

clean {
    dependsOn cleanJar
}

jar {
    manifest {
        attributes("MDW-Version": mdwVersion)
        attributes("MDW-Build": new Date().format(mdwBuildTimeFormat))
    }
    // exclude assets
    files { file("../${mdwAssetRoot}/com/centurylink/mdw").listFiles() }.each() {
        exclude "com/centurylink/mdw/${it.name}/"
    }
}

sourcesJar {
    // exclude assets
    files { file("../${mdwAssetRoot}/com/centurylink/mdw").listFiles() }.each() {
        exclude "com/centurylink/mdw/${it.name}/"
    }
}

archivesBaseName = "mdw-workflow"
artifacts {
    archives jar, javadocJar, sourcesJar
}