apply plugin: "osgi"

sourceSets {
    main {
        java {
            srcDir "xbean_src"
        }
        output.classesDir = "xbean_build"
    }
}

dependencies {
    compile group: "org.apache.xmlbeans", name: "xmlbeans", version: "2.4.0"
}

task xmlbeans << {
    ant.taskdef(name:"xmlbean", classname: "org.apache.xmlbeans.impl.tool.XMLBean", classpath: configurations.compile.asPath)
    ant.xmlbean(schema: "schemas", srcgendir: "xbean_src", classgendir: "${sourceSets.main.output.classesDir}", debug: "true", classpath: configurations.compile.asPath, javasource: "1.5" )
}

compileJava {
     dependsOn xmlbeans
}

task deleteXbeanSrc(type: Delete) {
    delete "xbean_src"
}

task deleteXbeanBin(type: Delete) {
    delete "xbean_build"
}

clean {
    dependsOn(deleteXbeanSrc, deleteXbeanBin, cleanJar)
}

jar {
    manifest {
        classesDir = sourceSets.main.output.classesDir
        symbolicName = "com.centurylink.mdw.schemas"
        instruction "Export-Package",
            "com.centurylink.*",
            "noNamespace.*",
            "org.wfmc.*",
            "schemaorg_apache_xmlbeans.*"
        instruction "Import-Package",
            "org.apache.xmlbeans.impl.schema",
            "*"
     }
}